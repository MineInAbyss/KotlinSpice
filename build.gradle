plugins {
    id 'java-platform'
    id 'maven-publish'
    id "com.github.ben-manes.versions" version "0.36.0"
    id "com.wynprice.cursemaven" version "2.1.5"
    id 'io.github.0ffz.github-packages' version '1.0.5'
}

allprojects {
    group 'com.mineinabyss'
    version kotlinVersion

    if (System.getenv("GITHUB_RUN_NUMBER") != null)
        version += "-" + System.getenv("GITHUB_RUN_NUMBER")
    else version += "-DEV"

    repositories {
        mavenCentral()
        jcenter()
        maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
        maven { url "https://minecraft.curseforge.com/api/maven/" }
        maven { url 'http://nexus.okkero.com/repository/maven-releases/' }
    }
}

dependencyUpdates.gradleReleaseChannel = "current"

javaPlatform {
    allowDependencies()
}

dependencies {
    api "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    api "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"

    api "org.jetbrains.kotlinx:kotlinx-serialization-json:1.0.1"
    api "com.charleskorn.kaml:kaml:0.26.0"
    api "org.jetbrains.kotlinx:kotlinx-coroutines-jdk8:1.4.1"

    constraints {
        //remember to add these to the spigot plugin's build.gradle as well
        api "com.okkero.skedule:skedule:1.2.6"

        api "org.jetbrains.kotlinx:kotlinx-serialization-cbor:1.0.1"

        api "org.nield:kotlin-statistics:1.2.1"
        api "org.jetbrains.exposed:exposed-dao:0.28.1"
        api "org.jetbrains.exposed:exposed-jdbc:0.28.1"
        api "org.xerial:sqlite-jdbc:3.32.3.2"
    }
}

publishing {
    repositories {
        maven githubPackagePublish.invoke("MineInAbyss/KotlinSpice")
    }
    publications {
        maven(MavenPublication) {
            from components.javaPlatform
        }
    }
}
