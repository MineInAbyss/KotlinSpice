plugins {
    id 'java-platform'
    id 'maven-publish'
    id "com.github.ben-manes.versions" version "0.39.0"
    id "com.wynprice.cursemaven" version "2.1.5"
}

allprojects {
    group 'com.mineinabyss'
    version kotlinVersion

    if (System.getenv("GITHUB_RUN_NUMBER") != null)
        version += "-" + System.getenv("GITHUB_RUN_NUMBER")
    else version += "-DEV"

    repositories {
        mavenCentral()
        jcenter()
        maven { url 'https://jitpack.io' }
        maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
        maven { url "https://minecraft.curseforge.com/api/maven/" }
    }
}

dependencyUpdates.gradleReleaseChannel = "current"

javaPlatform {
    allowDependencies()
}

dependencies {
//    compileOnly "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    api "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"

    api "org.jetbrains.kotlinx:kotlinx-serialization-json:1.2.1"
    api "com.charleskorn.kaml:kaml:0.34.0"
    api "org.jetbrains.kotlinx:kotlinx-coroutines-jdk8:1.5.0"

    constraints {
        //non-shaded dependencies
        api "de.erethon:headlib:3.0.6"

        //remember to add these to the spigot plugin's build.gradle as well
        api "com.github.okkero:skedule:1.2.6"

        api "org.jetbrains.kotlinx:kotlinx-serialization-cbor:1.2.1"

        api "org.nield:kotlin-statistics:1.2.1"
        api "org.jetbrains.exposed:exposed-dao:0.32.1"
        api "org.jetbrains.exposed:exposed-jdbc:0.32.1"
        api "org.xerial:sqlite-jdbc:3.34.0"
    }
}

publishing {
    repositories {
        maven {
            url = "https://repo.mineinabyss.com/releases"
            credentials {
                username = findProperty('mineinabyssMavenUsername')
                password = findProperty('mineinabyssMavenPassword')
            }
        }
    }
    publications {
        maven(MavenPublication) {
            from components.javaPlatform
        }
    }
}
