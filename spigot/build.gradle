import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id 'org.jetbrains.kotlin.jvm' version "$kotlinVersion"
}

java {
    sourceCompatibility(JavaVersion.VERSION_1_8)
    withSourcesJar()
}


dependencies {
    compileOnly "org.spigotmc:spigot-api:$server_version"
    api platform(project(':'))

    //TODO figure out if we can automatically add these dependencies from constraint
    api "com.okkero.skedule:skedule"

    api "org.jetbrains.kotlinx:kotlinx-serialization-cbor"

    api "org.nield:kotlin-statistics"
    api "org.jetbrains.exposed:exposed-dao"
    api "org.jetbrains.exposed:exposed-jdbc"
    api "org.xerial:sqlite-jdbc"
}

processResources {
    filter ReplaceTokens, tokens: [version: version]
}

shadowJar {
    archiveBaseName.set("KotlinSpice")
    mergeServiceFiles()
}


if (project.hasProperty("plugin_path") && plugin_path) {
    task copyJar(type: Copy) {
        from shadowJar
        into plugin_path
    }
    shadowJar.finalizedBy(copyJar)
    build.dependsOn copyJar
}

compileKotlin { kotlinOptions.jvmTarget = "1.8" }
compileTestKotlin { kotlinOptions.jvmTarget = "1.8" }
